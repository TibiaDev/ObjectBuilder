<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (c) 2014-2023 Object Builder <https://github.com/ottools/ObjectBuilder>

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
-->

<mg:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:mg="library://ns.mignari.com/mg"
    xmlns:otlib="otlib.components.*"
    width="500"
    height="400"
    fontSize="11"
    title="@Resource(key='compileAssetFiles', bundle='strings')"
    creationComplete="creationCompleteHandler(event)">

    <fx:Script>
        <![CDATA[
            import com.mignari.skins.PixeledBorderContainerSkin;
            import com.mignari.utils.DialogDetail;
            
            import mx.collections.ArrayCollection;
            import mx.events.FlexEvent;
            
            import spark.events.IndexChangeEvent;
            import spark.events.TextOperationEvent;
            
            import nail.utils.isNullOrEmpty;
            
            import otlib.core.Version;
            import otlib.core.VersionStorage;
            import otlib.events.FileTextInputEvent;
            import otlib.utils.OTFormat;
            
            // --------------------------------------------------------------------------
            // PROPERTIES
            // --------------------------------------------------------------------------
            
            private var m_filesName:String;
            private var m_filesNameChanged:Boolean;
            private var m_clientDirectory:File;
            private var m_clientDirectoryChanged:Boolean;
            private var m_sprFile:File;
            private var m_datFile:File;
            private var m_version:Version;
            private var m_versionChanged:Boolean;
            private var m_extended:Boolean;
            private var m_extendedChanged:Boolean;
            private var m_transparency:Boolean;
            private var m_transparencyChanged:Boolean;
            private var m_improvedAnimations:Boolean;
            private var m_improvedAnimationsChanged:Boolean;
            private var m_frameGroups:Boolean;
            private var m_frameGroupsChanged:Boolean;
            
            private var m_serverItemsDirectory:File;
            private var m_serverItemsDirectoryChanged:Boolean;
            private var m_serverItemsFormat:String = OTFormat.XML;
            private var m_serverItemsBinaryPeer:String = OTFormat.OTB;
            private var m_attributeServer:String = "tfs1.4";
            private var m_detail:uint;
            
            [Bindable]
            private var m_serverItemsLoaded:Boolean = false;
            
            // --------------------------------------
            // Getters / Setters
            // --------------------------------------
            
            public function get sprFile():File
            {
                return m_sprFile;
            }
            public function get datFile():File
            {
                return m_datFile;
            }
            
            public function get version():Version
            {
                return m_version;
            }
            public function set version(value:Version):void
            {
                if (value != m_version)
                {
                    m_version = value;
                    m_versionChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get filesName():String
            {
                return m_filesName;
            }
            public function set filesName(value:String):void
            {
                if (m_filesName != value)
                {
                    m_filesName = value;
                    m_filesNameChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get directory():File
            {
                return m_clientDirectory;
            }
            public function set directory(value:File):void
            {
                if (m_clientDirectory != value)
                {
                    m_clientDirectory = value;
                    m_clientDirectoryChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get extended():Boolean
            {
                return m_extended;
            }
            public function set extended(value:Boolean):void
            {
                if (value != m_extended)
                {
                    m_extended = value;
                    m_extendedChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get transparency():Boolean
            {
                return m_transparency;
            }
            public function set transparency(value:Boolean):void
            {
                if (value != m_transparency)
                {
                    m_transparency = value;
                    m_transparencyChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get improvedAnimations():Boolean
            {
                return m_improvedAnimations;
            }
            public function set improvedAnimations(value:Boolean):void
            {
                if (value != m_improvedAnimations)
                {
                    m_improvedAnimations = value;
                    m_improvedAnimationsChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get frameGroups():Boolean
            {
                return m_frameGroups;
            }
            public function set frameGroups(value:Boolean):void
            {
                if (value != m_frameGroups)
                {
                    m_frameGroups = value;
                    m_frameGroupsChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get detail():uint
            {
                return m_detail;
            }
            
            public function get attributeServer():String
            {
                return m_attributeServer;
            }
            public function set attributeServer(value:String):void
            {
                if (value && value != m_attributeServer)
                    m_attributeServer = value;
            }
            
            public function get serverItemsDirectory():File
            {
                return m_serverItemsDirectory;
            }
            public function set serverItemsDirectory(value:File):void
            {
                if (m_serverItemsDirectory != value)
                {
                    m_serverItemsDirectory = value;
                    m_serverItemsDirectoryChanged = true;
                    invalidateProperties();
                }
            }
            
            public function get serverItemsPath():String
            {
                return m_serverItemsDirectory ? m_serverItemsDirectory.nativePath : null;
            }
            
            public function get serverItemsFormat():String
            {
                return m_serverItemsFormat;
            }
            
            public function get serverItemsBinaryPeer():String
            {
                return m_serverItemsBinaryPeer;
            }
            
            public function set serverItemsLoaded(value:Boolean):void
            {
                m_serverItemsLoaded = value;
            }
            
            // --------------------------------------------------------------------------
            // METHODS
            // --------------------------------------------------------------------------
            
            // --------------------------------------
            // Override Protected
            // --------------------------------------
            
            override protected function commitProperties():void
            {
                super.commitProperties();
            
                if (m_filesNameChanged)
                {
                    setClientDirectory(m_clientDirectory);
                    m_filesNameChanged = false;
                }
            
                if (m_clientDirectoryChanged || m_versionChanged)
                {
                    setClientDirectory(m_clientDirectory);
                    m_clientDirectoryChanged = false;
                    m_versionChanged = false;
                }
            
                if (m_extendedChanged)
                {
                    setClientDirectory(m_clientDirectory);
                    m_extendedChanged = false;
                }
            
                if (m_transparencyChanged)
                {
                    updateProperties();
                    m_transparencyChanged = false;
                }
            
                if (m_improvedAnimationsChanged)
                {
                    updateProperties();
                    m_improvedAnimationsChanged = false;
                }
            
                if (m_frameGroupsChanged)
                {
                    updateProperties();
                    m_frameGroupsChanged = false;
                }
            
                if (m_serverItemsDirectoryChanged)
                {
                    setServerItemsDirectory(m_serverItemsDirectory);
                    m_serverItemsDirectoryChanged = false;
                }
            }
            
            // --------------------------------------
            // Private
            // --------------------------------------
            
            private function setClientDirectory(directory:File):void
            {
                m_datFile = null;
                m_sprFile = null;
            
                nameTextInput.text = m_filesName;
                directoryTextInput.file = directory;
                versionsDropDownList.selectedItem = m_version;
            
                if (directory && !isNullOrEmpty(m_filesName))
                {
                    m_datFile = directory.resolvePath(m_filesName + ".dat");
                    m_sprFile = directory.resolvePath(m_filesName + ".spr");
                }
            
                updateProperties();
            }
            
            private function setServerItemsDirectory(directory:File):void
            {
                serverItemsDirectoryInput.file = directory;
            }
            
            private function updateProperties():void
            {
                var done:Boolean = (m_datFile && m_sprFile && m_version);
                if (done)
                {
                    m_extended = (m_extended || m_version.value >= 960);
                    m_improvedAnimations = (m_improvedAnimations || m_version.value >= 1050);
                    extendedCheckBox.enabled = (m_version.value < 960);
                    transparencyCheckBox.enabled = true;
                    improvedAnimationsCheckBox.enabled = (m_version.value < 1050);
                    frameGroupsCheckBox.enabled = true;
                }
                else
                {
                    extendedCheckBox.enabled = false;
                    transparencyCheckBox.enabled = false;
                    improvedAnimationsCheckBox.enabled = false;
                    frameGroupsCheckBox.enabled = false;
                }
            
                extendedCheckBox.selected = m_extended;
                transparencyCheckBox.selected = m_transparency;
                improvedAnimationsCheckBox.selected = m_improvedAnimations;
                frameGroupsCheckBox.selected = m_frameGroups;
                confirmButton.enabled = done;
            }
            
            // --------------------------------------
            // Event Handlers
            // --------------------------------------
            
            private function creationCompleteHandler(event:FlexEvent):void
            {
                versionsDropDownList.dataProvider = new ArrayCollection(VersionStorage.getInstance().getList());
                versionsDropDownList.selectedItem = m_version;
            
                // Pre-populate server items directory if set
                if (m_serverItemsDirectory)
                {
                    serverItemsDirectoryInput.file = m_serverItemsDirectory;
                }
            
                focusManager.setFocus(confirmButton);
                focusManager.showFocus();
            }
            
            private function browseButtonClickHandler(event:MouseEvent):void
            {
                var path:String = m_clientDirectory ? m_clientDirectory.nativePath : File.userDirectory.nativePath;
                var dir:File = new File(path);
                dir.addEventListener(Event.SELECT, fileSelectHandler);
                dir.browseForDirectory(resourceManager.getString("strings", "selectFolder"));
            
                function fileSelectHandler(event:Event):void
                {
                    directory = dir;
                }
            }
            
            private function directorChangeHandler(event:FileTextInputEvent):void
            {
                this.directory = event.file;
            }
            
            private function nameTextInputChangeHandler(event:TextOperationEvent):void
            {
                this.filesName = nameTextInput.text;
            }
            
            private function versionsDropDownListChangeHandler(event:IndexChangeEvent):void
            {
                version = versionsDropDownList.selectedItem as Version;
            }
            
            private function extendedCheckBoxChangeHandler(event:Event):void
            {
                extended = extendedCheckBox.selected;
            }
            
            private function transparencyCheckBoxChangeHandler(event:Event):void
            {
                transparency = transparencyCheckBox.selected;
            }
            
            private function improvedAnimationsCheckBoxChangeHandler(event:Event):void
            {
                improvedAnimations = improvedAnimationsCheckBox.selected;
            }
            
            private function frameGroupsCheckBoxChangeHandler(event:Event):void
            {
                frameGroups = frameGroupsCheckBox.selected;
            }
            
            private function serverItemsEnableChangeHandler(event:Event):void
            {
                serverItemsBox.enabled = serverItemsEnableCheckBox.selected;
                if (!serverItemsEnableCheckBox.selected)
                    m_serverItemsDirectory = null;
                else
                    m_serverItemsDirectory = serverItemsDirectoryInput.file;
            }
            
            private function serverItemsDirectoryChangeHandler(event:Event):void
            {
                m_serverItemsDirectory = serverItemsDirectoryInput.file;
            }
            
            private function serverItemsFormatChangeHandler(event:IndexChangeEvent):void
            {
                var item:Object = serverItemsFormatList.selectedItem;
                if (item)
                    m_serverItemsFormat = item.value;
            }
            
            private function serverItemsPeerChangeHandler(event:IndexChangeEvent):void
            {
                var item:Object = serverItemsPeerList.selectedItem;
                if (item)
                    m_serverItemsBinaryPeer = item.value;
            }
            
            private function browseServerItemsButtonClickHandler(event:MouseEvent):void
            {
                var path:String = m_serverItemsDirectory ? m_serverItemsDirectory.nativePath : File.userDirectory.nativePath;
                var dir:File = new File(path);
                dir.addEventListener(Event.SELECT, fileSelectHandler);
                dir.browseForDirectory(resourceManager.getString("strings", "selectFolder"));
            
                function fileSelectHandler(event:Event):void
                {
                    serverItemsDirectory = dir;
                }
            }
            
            private function confirmButtonClickHandler(event:MouseEvent):void
            {
                m_detail = DialogDetail.OK;
                close();
            }
            
            private function cancelButtonClickHandler(event:MouseEvent):void
            {
                m_detail = DialogDetail.CANCEL;
                close();
            }
        ]]>
    </fx:Script>

    <mg:layout>
        <s:VerticalLayout/>
    </mg:layout>

    <s:VGroup width="100%" height="100%" gap="10" padding="10">
        <!-- name -->
        <s:Group width="100%">
            <s:layout>
                <s:VerticalLayout gap="2"/>
            </s:layout>

            <s:Label text="{resourceManager.getString('strings', 'name') + ':'}"
                fontSize="11"
                color="0xDFDFDF"/>

            <s:TextInput id="nameTextInput"
                width="100%"
                change="nameTextInputChangeHandler(event)"/>
        </s:Group>

        <!-- output folder -->
        <s:Group width="100%">
            <s:layout>
                <s:VerticalLayout gap="2"/>
            </s:layout>

            <s:Label text="{resourceManager.getString('strings', 'outputFolder') + ':'}"
                fontSize="11"
                color="0xDFDFDF"/>

            <s:HGroup width="100%" height="100%">
                <otlib:FileTextInput id="directoryTextInput"
                    width="100%"
                    fileChange="directorChangeHandler(event)"/>

                <s:Button id="browseButton"
                    label="@Resource(key='browse', bundle='strings')"
                    minWidth="70"
                    height="21"
                    click="browseButtonClickHandler(event)"/>
            </s:HGroup>
        </s:Group>

        <!-- Server Items Export (under output folder) -->
        <s:Group width="100%">
            <s:layout>
                <s:VerticalLayout gap="2"/>
            </s:layout>

            <s:Label text="Server Items Export:"
                fontSize="11"
                color="0xDFDFDF"/>

            <s:VGroup width="100%" gap="5">
                <s:CheckBox id="serverItemsEnableCheckBox"
                    label="Enable Export"
                    enabled="{m_serverItemsLoaded}"
                    change="serverItemsEnableChangeHandler(event)"/>

                <s:VGroup id="serverItemsBox" width="100%" gap="5" enabled="false">
                    <s:HGroup width="100%" verticalAlign="middle">
                        <otlib:FileTextInput id="serverItemsDirectoryInput"
                            width="100%"
                            fileChange="serverItemsDirectoryChangeHandler(event)"/>

                        <s:Button label="@Resource(key='browse', bundle='strings')"
                            height="21"
                            click="browseServerItemsButtonClickHandler(event)"/>
                    </s:HGroup>

                    <s:HGroup gap="10" width="100%" verticalAlign="middle">
                        <s:Label text="@Resource(key='definitions', bundle='strings')"/>
                        <s:DropDownList id="serverItemsFormatList" width="100%" change="serverItemsFormatChangeHandler(event)" requireSelection="true">
                            <s:dataProvider>
                                <s:ArrayList>
                                    <fx:Object label="XML" value="xml"/>
                                    <!-- TODO: TOML support not yet implemented
                                    <fx:Object label="TOML" value="toml"/> -->
                                </s:ArrayList>
                            </s:dataProvider>
                        </s:DropDownList>

                        <s:Label text="@Resource(key='binary', bundle='strings')"/>
                        <s:DropDownList id="serverItemsPeerList" width="100%" change="serverItemsPeerChangeHandler(event)" requireSelection="true">
                            <s:dataProvider>
                                <s:ArrayList>
                                    <fx:Object label="Items.otb" value="otb"/>
                                    <fx:Object label="Tibia.dat" value="dat"/>
                                    <fx:Object label="Assets.dat" value="assets"/>
                                </s:ArrayList>
                            </s:dataProvider>
                        </s:DropDownList>
                    </s:HGroup>
                </s:VGroup>
            </s:VGroup>
        </s:Group>

        <!-- settings grid (version) -->
        <mg:GroupBox width="100%" label="Settings">
            <mg:layout>
                <s:TileLayout requestedColumnCount="2" horizontalGap="20" verticalGap="10" padding="10" columnAlign="justifyUsingWidth"/>
            </mg:layout>

            <s:VGroup gap="2" width="100%">
                <s:Label text="@Resource(key='version', bundle='strings')"/>
                <s:DropDownList id="versionsDropDownList"
                    width="100%" height="21"
                    change="versionsDropDownListChangeHandler(event)"/>
            </s:VGroup>
        </mg:GroupBox>

        <!-- client options -->
        <mg:GroupBox width="100%" label="@Resource(key='options', bundle='strings')">
            <mg:layout>
                <s:TileLayout requestedColumnCount="2" horizontalGap="10" verticalGap="5" padding="10"/>
            </mg:layout>

            <s:CheckBox id="extendedCheckBox"
                label="@Resource(key='extended', bundle='strings')"
                enabled="false"
                change="extendedCheckBoxChangeHandler(event)"/>

            <s:CheckBox id="transparencyCheckBox"
                label="@Resource(key='transparency', bundle='strings')"
                change="transparencyCheckBoxChangeHandler(event)"/>

            <s:CheckBox id="improvedAnimationsCheckBox"
                label="@Resource(key='improvedAnimations', bundle='strings')"
                change="improvedAnimationsCheckBoxChangeHandler(event)"/>

            <s:CheckBox id="frameGroupsCheckBox"
                label="@Resource(key='frameGroups', bundle='strings')"
                change="frameGroupsCheckBoxChangeHandler(event)"/>
        </mg:GroupBox>
    </s:VGroup>

    <s:BorderContainer width="100%" skinClass="com.mignari.skins.PixeledBorderContainerSkin">
        <s:layout>
            <s:HorizontalLayout gap="10" padding="10" verticalAlign="middle" horizontalAlign="right"/>
        </s:layout>

        <s:Button id="confirmButton"
            label="@Resource(key='confirm', bundle='strings')"
            minWidth="70"
            enabled="false"
            click="confirmButtonClickHandler(event)"/>

        <s:Button id="cancelButton"
            label="@Resource(key='cancel', bundle='strings')"
            minWidth="70"
            click="cancelButtonClickHandler(event)"/>
    </s:BorderContainer>
</mg:TitleWindow>
