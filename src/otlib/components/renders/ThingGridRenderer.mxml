<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (c) 2014-2023 Object Builder <https://github.com/ottools/ObjectBuilder>

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
-->

<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:nail="library://ns.nail.com/naillib"
    width="64" height="71"
    fontSize="9"
    creationComplete="creationCompleteHandler(event)"
    dataChange="dataChangeHandler(event)">

    <fx:Script>
        <![CDATA[
            import mx.events.FlexEvent;
            import mx.resources.IResourceManager;
            import mx.resources.ResourceManager;
            
            import nail.logging.Log;
            import otlib.animation.FrameGroup;
            import otlib.components.ListBase;
            import otlib.components.ThingList;
            import otlib.core.otlib_internal;
            import otlib.events.ThingListEvent;
            import otlib.things.ThingType;
            import otlib.things.FrameGroupType;
            import otlib.utils.ThingListItem;
            import nail.utils.isNullOrEmpty;
            
            use namespace otlib_internal;
            
            // --------------------------------------------------------------------------
            // METHODS
            // --------------------------------------------------------------------------
            
            // --------------------------------------
            // Event Handlers
            // --------------------------------------
            
            protected function creationCompleteHandler(event:FlexEvent):void
            {
                if (owner is ListBase && ListBase(owner).contextMenuEnabled)
                {
                    var cm:ContextMenu = createContextMenu();
                    cm.addEventListener(Event.SELECT, contextMenuSelectHandler);
                    cm.addEventListener(Event.DISPLAYING, contextMenuDisplayingHandler);
                    this.contextMenu = cm;
                }
            }
            
            protected function contextMenuDisplayingHandler(event:Event):void
            {
                var menu:ContextMenu = ContextMenu(event.target);
                var item:ThingListItem = this.data as ThingListItem;
                if (item && item.thing)
                {
                    var resource:IResourceManager = ResourceManager.getInstance();
                    // Update Copy ID menu item label with the actual ID (last item in the menu)
                    var copyIdItem:ContextMenuItem = menu.customItems[menu.customItems.length - 1] as ContextMenuItem;
                    if (copyIdItem)
                    {
                        copyIdItem.caption = resource.getString("strings", "copyId", [item.thing.id]);
                    }
                }
                if (owner is ThingList)
                {
                    ThingList(owner).onContextMenuDisplaying(this.itemIndex, menu);
                }
            }
            
            protected function contextMenuSelectHandler(event:Event):void
            {
                if (owner is ThingList)
                {
                    var type:String = NativeMenuItem(event.target).data as String;
                    ThingList(owner).onContextMenuSelect(this.itemIndex, type);
                }
            }
            
            protected function dataChangeHandler(event:FlexEvent):void
            {
                var item:ThingListItem = this.data as ThingListItem;
                if (item)
                {
                    var thing:ThingType = item.thing;
                    idLabel.text = thing.id.toString();
            
                    try
                    {
                        imageDisplay.source = item.getBitmap();
                    }
                    catch (error:Error)
                    {
                        Log.error(error.message, error.getStackTrace(), error.errorID);
                    }
                }
            }
            
            // --------------------------------------------------------------------------
            // STATIC
            // --------------------------------------------------------------------------
            
            private static function createContextMenu():ContextMenu
            {
                var resource:IResourceManager = ResourceManager.getInstance();
                var replaceMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "replace"));
                replaceMenu.data = ThingListEvent.REPLACE;
                var exportMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "export"));
                exportMenu.data = ThingListEvent.EXPORT;
                var editMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "edit"));
                editMenu.data = ThingListEvent.EDIT;
                var duplicateMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "duplicate"));
                duplicateMenu.data = ThingListEvent.DUPLICATE;
                var bulkEditMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "bulkEdit"));
                bulkEditMenu.data = ThingListEvent.BULK_EDIT;
                var copyObjectMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "copyObject"));
                copyObjectMenu.data = ThingListEvent.COPY_OBJECT;
                var pasteObjectMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "pasteObject"));
                pasteObjectMenu.data = ThingListEvent.PASTE_OBJECT;
                var copyAttributesMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "copyAttributes"));
                copyAttributesMenu.data = ThingListEvent.COPY_ATTRIBUTES;
                var pasteAttributesMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "pasteAttributes"));
                pasteAttributesMenu.data = ThingListEvent.PASTE_ATTRIBUTES;
                var removeMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "remove"));
                removeMenu.data = ThingListEvent.REMOVE;
                var copyIdMenu:ContextMenuItem = new ContextMenuItem(resource.getString("strings", "copyId"), true);
                copyIdMenu.data = ThingListEvent.COPY_ID;
                var menu:ContextMenu = new ContextMenu();
                menu.customItems = [replaceMenu, exportMenu, editMenu, duplicateMenu, bulkEditMenu, copyObjectMenu, pasteObjectMenu, copyAttributesMenu, pasteAttributesMenu, removeMenu, copyIdMenu];
                return menu;
            }
        ]]>
    </fx:Script>

    <!-- states -->
    <s:states>
        <s:State name="normal"/>
        <s:State name="hovered"/>
        <s:State name="selected"/>
    </s:states>

    <!-- fill -->
    <s:Rect left="1" right="1" top="1" bottom="1">
        <s:fill>
            <s:SolidColor color="0x535353" color.hovered="0x3385B2" color.selected="0x156692"/>
        </s:fill>
    </s:Rect>

    <!-- border -->
    <s:Rect left="0" right="1" top="0" bottom="0">
        <s:stroke>
            <s:SolidColorStroke color="0x272727" weight="0.1"/>
        </s:stroke>
    </s:Rect>

    <!-- image background -->
    <s:Rect horizontalCenter="0" top="3" width="56" height="56">
        <s:fill>
            <s:SolidColor color="0x636363"/>
        </s:fill>
        <s:stroke>
            <s:SolidColorStroke color="0x272727"/>
        </s:stroke>
    </s:Rect>

    <!-- image -->
    <s:BitmapImage id="imageDisplay" horizontalCenter="0" top="4" width="54" height="54" fillMode="scale" scaleMode="letterbox"/>

    <!-- ID text -->
    <s:Label id="idLabel" bottom="2" horizontalCenter="0" textAlign="center" mouseChildren="false" mouseEnabled="false"/>
</s:ItemRenderer>
